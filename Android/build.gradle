apply plugin: 'com.android.application'

dependencies {
    compile 'com.google.android.gms:play-services:5.0.89'
    compile 'com.sothree.slidinguppanel:library:2.0.2'
    compile 'com.android.support:support-v4:20.0.0'

    compile 'com.o3dr:3dr-services-client:2.1.6'
    compile 'com.o3dr:3dr-services-library:2.0.13'
    compile 'com.o3dr:mavlink:0.0.1'

    compile files('libs/droneapi-java.jar')
    compile files('libs/j2xx.jar')
    compile files('libs/protobuf-java-2.5.0.jar')
    compile files('libs/jeromq-0.3.4.jar')

    compile project(':Libraries:PebbleKit')
    compile project(':Libraries:HorizontalVariableListView:HorizontalVariableListView')
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId 'org.droidplanner.android'
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 308
        versionName "alpha v3.0.8"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        androidTest {
            java.srcDirs = ['test']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    //FIXME: remove this after lint errors have been taken care of
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile getAppKeystoreFile()
            storePassword getAppKeystorePassword()
            keyAlias getAppKey()
            keyPassword getAppKeyPassword()
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.apk')) {
                def buildType = variant.buildType.getName()
                def versionCode = defaultConfig.versionCode
                output.outputFile = new File(file.parent,
                        "droidplanner-3-${buildType}.${versionCode}.apk")
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

/**
 * @return The most recent git tag to be used as version name for the app.
 */
def getGitVersion(){
    def cmd = "git describe --tag --dirty"
    try {
        def proc = cmd.execute()
        return proc.text.trim()
    }catch(IOException e){
        //Unable to find 'git'
        return "please update version name manually"
    }
}
